import NagataFactoriality.Basic.Divisibility

namespace NagataFactoriality

namespace Submonoid

variable {α : Type*} [CommRing α] [IsDomain α]

theorem zero_notMem_of_prime_or_unit {S : Submonoid α}
    (hS : ∀ s ∈ S, Prime s ∨ IsUnit s) : (0 : α) ∉ S := by
  intro h0
  rcases hS 0 h0 with h0prime | h0unit
  · exact h0prime.ne_zero rfl
  · exact h0unit.ne_zero rfl

theorem mem_ne_zero {S : Submonoid α} [Fact ((0 : α) ∉ S)] {s : α} (hs : s ∈ S) : s ≠ 0 := by
  intro hs0
  have h0 : (0 : α) ∈ S := by simpa [hs0] using hs
  exact (Fact.out : (0 : α) ∉ S) h0

theorem le_nonZeroDivisors {S : Submonoid α} [Fact ((0 : α) ∉ S)] :
    S ≤ nonZeroDivisors α := by
  intro s hs
  rw [mem_nonZeroDivisors_iff_ne_zero]
  exact mem_ne_zero hs

end Submonoid

end NagataFactoriality
